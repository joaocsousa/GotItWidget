apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "0.5"

ext.propertiesFile = "OneTimeHintView.properties";
Properties properties = new Properties()

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

assembleRelease.doLast {
    if (checkProjectProperties()) {
        properties.load(new FileInputStream(file(project.property(propertiesFile))))
    }
}

boolean checkProjectProperties() {
    if(!project.hasProperty(propertiesFile)) {
        println "Property " + propertiesFile + " doesn't exist in the gradle.build file";
        if (!new File(project.property(propertiesFile)).exists()) {
            println "File " + project.property(propertiesFile) + " doesn't exist";
        }
    }
    return project.hasProperty(propertiesFile) && new File(project.property(propertiesFile)).exists()
}

def siteUrl = 'https://github.com/joaocsousa/OneTimeHintView'      // Homepage URL of the library
def gitUrl = 'https://github.com/joaocsousa/OneTimeHintView.git'   // Git repository URL
group = "com.github.joaocsousa"                                    // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Hint view that is displayed until acknowledged.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id properties.sonatypeUsername
                        name properties.sonatypeName
                        email properties.sonatypeEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties.bintrayUser
    key = properties.bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "OneTimeHintView"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:support-annotations:21.0.3'
}
