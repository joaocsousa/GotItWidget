apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "1.0.0"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
    }
    buildTypes {
    }
}

def siteUrl = 'https://github.com/joaocsousa/OneTimeHintView'      // Homepage URL of the library
def gitUrl = 'https://github.com/joaocsousa/OneTimeHintView.git'   // Git repository URL
group = "com.github.joaocsousa"                                    // Maven Group ID for the artifact

def hasSonatypeConfig = project.hasProperty("sonatypeUsername") &&
        project.hasProperty("sonatypeName") &&
        project.hasProperty("sonatypeEmail")

def hasBintrayConfig = project.hasProperty("bintrayUser") &&
        project.hasProperty("bintrayApiKey")

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'OneTimeHintView'
                description 'Android library that displays a dismissable view until aknowledged.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        if (hasSonatypeConfig) {
                            id sonatypeUsername
                            name sonatypeName
                            email sonatypeEmail
                        }
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    if (hasBintrayConfig) {
        user = bintrayUser
        key = bintrayApiKey
    }
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "OneTimeHintView"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:support-annotations:21.0.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}
